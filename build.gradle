import java.nio.charset.StandardCharsets

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'org.ajoberstar.reckon' version '0.13.1'
}

def generalCharset = StandardCharsets.UTF_8.name();

reckon {
    scopeFromProp()
    stageFromProp('dev', 'beta', 'rc', 'final')
}

group = pluginGroup

def targetJavaVersion = 17
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

compileJava.options.encoding = generalCharset
compileTestJava.options.encoding = generalCharset
javadoc.options.encoding = generalCharset

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    mavenLocal()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = project.name
                description = 'LeafCore is a library plugin offering tools for plugin developers, making some aspect of the plugin development way easier.'

                url = 'https://github.com/leaf-mc/leaf-core'

                licenses {
                    license {
                        name = 'Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'alexpado'
                        name = 'Alexandre PADOVAN'
                        email = 'contact@alexpado.fr'
                    }
                }

                scm {
                    connection = 'scm:git:github.com/leaf-mc/leaf-core.git'
                    developerConnection = 'scm:git:ssh://github.com/leaf-mc/leaf-core.git'
                    url = 'https://github.com/leaf-mc/leaf-core/tree/main'
                }
            }
        }
    }
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.0'
    compileOnly group: 'io.papermc.paper', name: 'paper-api', version: '1.19.2-R0.1-SNAPSHOT'
    implementation group: 'org.jetbrains', name: 'annotations', version: '23.0.0'
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

tasks.withType(Jar) {
    if (project.properties.containsKey("workspace")) {
        destinationDir = file("$rootDir/.papermc/plugins")
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
